buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply plugin: 'com.android.application'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.android.support:support-v4:23.1.1'
}

if (file("./signing.properties").exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    android {
        signingConfigs {
            release {
                storeFile file(props['storeFileName'])
                keyAlias props['storeAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }
        testBuildType "release"
        buildTypes {
            release {
                minifyEnabled true
                zipAlignEnabled true
                proguardFile './proguard.cfg'
                signingConfig signingConfigs.release
            }
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['assets']
        }

        androidTest {
            java.srcDirs = ['Test/src']
            resources.srcDirs = ['Test/src']
            res.srcDirs = ['Test/res']
            jniLibs.srcDirs = ['Test/libs']
            assets.srcDirs = ['Test/assets']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        versionCode 250
        versionName "2.5.0"
        applicationId "ru.valle.btc"
        testApplicationId "ru.valle.btc.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testFunctionalTest true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
