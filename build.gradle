buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}
repositories {
    jcenter()
}

apply plugin: 'com.android.application'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.madgag.spongycastle:core:1.54.0.0'
    compile 'com.android.support:support-annotations:25.3.1'
}

if (file("./signing.properties").exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    android {
        signingConfigs {
            release {
                storeFile file(props['storeFileName'])
                keyAlias props['storeAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }
        testBuildType "release"
        buildTypes {
            release {
                minifyEnabled true
                zipAlignEnabled true
                proguardFile './proguard.cfg'
                signingConfig signingConfigs.release
            }
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['assets']
        }

        androidTest {
            java.srcDirs = ['Test/src']
            resources.srcDirs = ['Test/src']
            res.srcDirs = ['Test/res']
            jniLibs.srcDirs = ['Test/libs']
            assets.srcDirs = ['Test/assets']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        versionCode 260
        versionName "2.6.0"
        applicationId "ru.valle.btc"
        testApplicationId "ru.valle.btc.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testFunctionalTest true
    }
    //noinspection GroovyAssignabilityCheck
    buildTypes {
        debug {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        //TrulyRandom is to mute warning about java.util.SecureRandom in TrulySecureRandom class. It's totally okay for that use case.
        disable 'TrulyRandom', "GoogleAppIndexingWarning"
    }
}
